ext {
    versionName = getVersionName()
    versionCode = getVersionCode()

    gradleDir = "${rootDir}/gradle"

    gradlePlugins = [
            android: "com.android.tools.build:gradle:3.1.3",
            projectInfo: "${gradleDir}/project-info.gradle",
            mavenLocalPublish: "${gradleDir}/maven-local-publish.gradle"
    ]

    versions = [
            compileSdk: 27,
            minSdk: 14,
            targetSdk: 27,

            supportLibrary: '27.1.1',
            butterKnife: '8.8.1',
    ]

    deps = [
            test: [
                    junit: 'junit:junit:4.12',
                    androidJunit: 'com.android.support.test:runner:1.0.2',
                    espresso: 'com.android.support.test.espresso:espresso-core:3.0.2',
            ],
            support: [
                    appcompat: "com.android.support:appcompat-v7:${versions.supportLibrary}",
                    cardview: "com.android.support:cardview-v7:${versions.supportLibrary}",
                    recyclerview: "com.android.support:recyclerview-v7:${versions.supportLibrary}",
            ],
            butterknife: "com.jakewharton:butterknife:${versions.butterKnife}",
            butterknifeCompiler: "com.jakewharton:butterknife-compiler:${versions.butterKnife}",
    ]

    runners = [
            androidJunit: 'android.support.test.runner.AndroidJUnitRunner',
    ]
}

def getVersionName() {
    def cmd = 'git describe --tags'
    cmd.execute().text.trim()
}

def getVersionCode() {
    // Get git commit count as version code.
    def cmd = 'git rev-list HEAD --count'
    cmd.execute().text.trim().toInteger()
}
